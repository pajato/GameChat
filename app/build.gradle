apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'project-report'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.pajato.android.gamechat"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName "b1.$versionCode"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(GC_STORE_FILE)
            storePassword GC_STORE_PASSWORD
            keyAlias GC_RELEASE_KEY_ALIAS
            keyPassword GC_RELEASE_KEY_PASSWORD
            v2SigningEnabled false
        }
        stage {
            storeFile file(GC_STORE_FILE)
            storePassword GC_STORE_PASSWORD
            keyAlias GC_STAGE_KEY_ALIAS
            keyPassword GC_STAGE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            debuggable true
            shrinkResources true
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled = true
        }
        release {
            shrinkResources true
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        stage {
            initWith debug
            signingConfig signingConfigs.stage
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'GC_TEST_EMAIL_KEY', GC_TEST_EMAIL
        it.buildConfigField 'String', 'GC_TEST_PASSWORD_KEY', GC_TEST_PASSWORD
        it.buildConfigField 'String', 'GC_TEST_PROVIDER_KEY', GC_TEST_PROVIDER
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        abortOnError true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

dependencies {

    // Android
    implementation "com.android.support:cardview-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:customtabs:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:multidex:1.0.1'

    // Google
    implementation "com.google.android.gms:play-services-ads:$rootProject.ext.gmsVersion"
    implementation "com.google.android.gms:play-services-appinvite:$rootProject.ext.gmsVersion"
    implementation "com.google.android.gms:play-services-auth:$rootProject.ext.gmsVersion"

    // Firebase
    implementation "com.google.firebase:firebase-analytics:$rootProject.ext.gmsVersion"
    implementation "com.google.firebase:firebase-auth:$rootProject.ext.gmsVersion"
    implementation "com.google.firebase:firebase-database:$rootProject.ext.gmsVersion"
    implementation "com.google.firebase:firebase-messaging:$rootProject.ext.gmsVersion"
    implementation 'com.firebase:firebase-client-android:2.4.0'
    implementation "com.google.firebase:firebase-invites:$rootProject.ext.gmsVersion"

    // Firebase UI
    implementation "com.firebaseui:firebase-ui-auth:$rootProject.ext.firebaseAuthUIVersion"

    // External
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'de.hdodenhof:circleimageview:1.3.0'
    implementation 'org.greenrobot:eventbus:3.0.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Espresso UI testing dependencies.
    androidTestImplementation "com.android.support:support-annotations:$rootProject.ext.supportLibraryVersion"
    androidTestImplementation "com.android.support:support-v4:$rootProject.ext.supportLibraryVersion"
    androidTestImplementation "com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion"
    androidTestImplementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    androidTestImplementation "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"

    // UIAutomator testing dependencies.
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$rootProject.ext.uiautomatorVersion"
}

// Per Firebase instructions this comes last.
apply plugin: 'com.google.gms.google-services'
